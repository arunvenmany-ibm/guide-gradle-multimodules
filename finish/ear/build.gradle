// tag::ear[]
apply plugin: 'ear'
// end::ear[]
// tag::liberty[]
apply plugin: 'liberty'
// end::liberty[]

description = 'EAR Module'

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name = 'Sonatype Nexus Snapshots'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
    dependencies {
        classpath "io.openliberty.tools:liberty-gradle-plugin:3.9.2"
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // tag::war[]
    deploy project(path:':war', configuration:'archives')
    // end::war[]
    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.3'
}

// tag::earConfig[]
ear {
    archiveFileName = rootProject.name + "-" + getArchiveBaseName().get() + "-" +
                      rootProject.version + '.' + getArchiveExtension().get()
    deploymentDescriptor {
        webModule ('guide-gradle-multimodules-war-1.0-SNAPSHOT.war', '/converter')
    }
}
// end::earConfig[]

// tag::libertyConfig[]
liberty {
    server {
        name = "sampleLibertyServer"
        deploy {
            apps = [ear]
            copyLibsDirectory = file("${project.getLayout().getBuildDirectory().getAsFile().get()}/libs")
        }
        verifyAppStartTimeout = 30
        looseApplication = true
    }
}
// end::libertyConfig[]

// tag::deployDependsOn[]
deploy.dependsOn 'ear'
// end::deployDependsOn[]
// tag::earDependsOn[]
ear.dependsOn ':jar:jar', ':war:war'
// end::earDependsOn[]
