// tag::ear[]
apply plugin: 'ear'
// end::ear[]
// tag::liberty[]
apply plugin: 'liberty'
// end::liberty[]

description = 'EAR Module'

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name = 'Sonatype Nexus Snapshots'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
    dependencies {
        classpath 'io.openliberty.tools:liberty-gradle-plugin:3.9.2'
    }
}


dependencies {
    // tag::war[]
    deploy project(path:':war', configuration:'archives')
    // end::war[]
}

// tag::earConfig[]
ear {
    archiveFileName = rootProject.name + '-' + getArchiveBaseName().get() + '-' +
                      rootProject.version + '.' + getArchiveExtension().get()
    deploymentDescriptor {
        // tag::webModule[]
        webModule ('guide-gradle-multimodules-war-1.0-SNAPSHOT.war', '/converter')
        // end::webModule[]
    }
}
// end::earConfig[]

// tag::libertyConfig[]
liberty {
    server {
        // tag::sampleLibertyServer[]
        name = 'sampleLibertyServer'
        // end::sampleLibertyServer[]
        deploy {
            apps = [ear]
            // tag::copyLibsDirectory[]
            copyLibsDirectory = file("${project.getLayout().getBuildDirectory().getAsFile().get()}/libs")
            // end::copyLibsDirectory[]
        }
        var.'http.port' = '9080'
        var.'https.port' = '9443'
        verifyAppStartTimeout = 30
        looseApplication = true
    }
}
// end::libertyConfig[]

// tag::test[]
test {
    systemProperty 'http.port', liberty.server.var.'http.port'
}
// end::test[]

// tag::deployDependsOn[]
deploy.dependsOn 'ear'
// end::deployDependsOn[]
// tag::earDependsOn[]
ear.dependsOn ':jar:jar', ':war:war'
// end::earDependsOn[]
